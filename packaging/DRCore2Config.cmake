cmake_minimum_required(VERSION 3.18.2)

set(DRCore2_known_comps static shared)
set(DRCore2_comp_static NO)
set(DRCore2_comp_shared NO)
foreach (DRCore2_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (DRCore2_comp IN_LIST DRCore2_known_comps)
        set(DRCore2_comp_${DRCore2_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "DRCore2 does not recognize component `${DRCore2_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (DRCore2_comp_static AND DRCore2_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "DRCore2 `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(DRCore2_static_targets "${CMAKE_CURRENT_LIST_DIR}/DRCore2-static-targets.cmake")
set(DRCore2_shared_targets "${CMAKE_CURRENT_LIST_DIR}/DRCore2-shared-targets.cmake")

macro(DRCore2_load_targets type)
    if (NOT EXISTS "${DRCore2_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "DRCore2 `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${DRCore2_${type}_targets}")
endmacro()

if (DRCore2_comp_static)
    DRCore2_load_targets(static)
elseif (DRCore2_comp_shared)
    DRCore2_load_targets(shared)
elseif (DEFINED DRCore2_SHARED_LIBS AND DRCore2_SHARED_LIBS)
    DRCore2_load_targets(shared)
elseif (DEFINED DRCore2_SHARED_LIBS AND NOT DRCore2_SHARED_LIBS)
    DRCore2_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${DRCore2_shared_targets}")
        DRCore2_load_targets(shared)
    else ()
        DRCore2_load_targets(static)
    endif ()
else ()
    if (EXISTS "${DRCore2_static_targets}")
        DRCore2_load_targets(static)
    else ()
        DRCore2_load_targets(shared)
    endif ()
endif ()